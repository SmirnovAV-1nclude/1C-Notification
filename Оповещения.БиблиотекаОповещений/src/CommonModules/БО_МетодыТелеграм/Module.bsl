
#Область ПрограммныйИнтерфейс

// Параметры взаимодействия.
// 
// Параметры:
//  Токен - Неопределено, Строка - Токен
//  ИдентификаторПолучатель - Неопределено, Строка - Идентификатор получатель
// 
// Возвращаемое значение:
//  Структура - Параметры взаимодействия:
// * Токен - Неопределено, Строка - 
// * ИдентификаторПолучатель - Неопределено, Строка - 
Функция ПараметрыВзаимодействия(Токен = Неопределено, ИдентификаторПолучатель = Неопределено) Экспорт
	
	ПараметрыВзаимодействия = Новый Структура();
	ПараметрыВзаимодействия.Вставить("Токен", Токен);
	ПараметрыВзаимодействия.Вставить("ИдентификаторПолучатель", ИдентификаторПолучатель);
	
	Возврат ПараметрыВзаимодействия;
	
КонецФункции

// Get me.
// 
// Параметры:
//  ПараметрыВзаимодействия - Структура - См. ПараметрыВзаимодействия()
//  Сессия - Неопределено, Структура - См. КоннекторHTTP.СоздатьСессию()
// 
// Возвращаемое значение:
//  
Функция getMe(ПараметрыВзаимодействия, Сессия = Неопределено) Экспорт
	
	ИнициализироватьСессию(Сессия);
	
	URL = СтрШаблон("%1bot%2/getMe", АдресРесурса(), ПараметрыВзаимодействия.Токен);
	Возврат КоннекторHTTP.GetJson(URL, Неопределено, Неопределено, Сессия);
	
КонецФункции
  
Функция sendMessage(ПараметрыВзаимодействия, ТекстСообщения, Сессия = Неопределено) Экспорт
	
	ИнициализироватьСессию(Сессия);
	
	URL = СтрШаблон("%1bot%2/sendMessage", АдресРесурса(), ПараметрыВзаимодействия.Токен);
	
	ПараметрыЗапроса = Новый Структура();
	ПараметрыЗапроса.Вставить("parse_mode", "MarkdownV2");
	ПараметрыЗапроса.Вставить("chat_id", ПараметрыВзаимодействия.ИдентификаторПолучатель);
	ПараметрыЗапроса.Вставить("text", ТекстСообщения);
	
	Возврат КоннекторHTTP.GetJson(URL, ПараметрыЗапроса, Неопределено, Сессия);	
	
КонецФункции

// Send photo.
// 
// Параметры:
//  ПараметрыВзаимодействия - Структура - См. ПараметрыВзаимодействия()
//  Файл - Структура - Описание файла
//  	* Данные - ДвоичныеДанные - 
//  	* ИмяФайла - Строка - Имя отправляемого файла
//  ТекстовоеОписание - Строка - Подпись к отправляемой фотографии
//  Сессия - Неопределено, Структура - См. КоннекторHTTP.СоздатьСессию()
// 
// Возвращаемое значение:
//  
Функция sendPhoto(ПараметрыВзаимодействия, Файл, ТекстовоеОписание = "", Сессия = Неопределено) Экспорт
	
	ИнициализироватьСессию(Сессия);
	
	URL = СтрШаблон("%1bot%2/sendPhoto", АдресРесурса(), ПараметрыВзаимодействия.Токен);
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("chat_id", ПараметрыВзаимодействия.ИдентификаторПолучатель);
	ПараметрыЗапроса.Вставить("parse_mode", "MarkdownV2");
	ПараметрыЗапроса.Вставить("caption", ТекстовоеОписание);
	
	ДополнительныеПараметры = КоннекторHTTP.НовыеПараметры();
	ДополнительныеПараметры.Данные = ПараметрыЗапроса;
	ДополнительныеПараметры.Файлы = ОписаниеФайла(Файл, "photo");
	
	Возврат КоннекторHTTP.PostJson(URL, Неопределено, ДополнительныеПараметры, Сессия);
	
КонецФункции

// Send audio.
// 
// Параметры:
//  ПараметрыВзаимодействия - Структура - См. ПараметрыВзаимодействия()
//  Файл - Структура - Описание файла
//  	* Данные - ДвоичныеДанные - 
//  	* ИмяФайла - Строка - Имя отправляемого файла
//  ТекстовоеОписание - Строка - Текстовое описание
//  Сессия - Неопределено, Структура - См. КоннекторHTTP.СоздатьСессию()
// 
// Возвращаемое значение:
//  
Функция sendAudio(ПараметрыВзаимодействия, Файл, ТекстовоеОписание = "", Сессия = Неопределено) Экспорт
	
	ИнициализироватьСессию(Сессия);
	
	URL = СтрШаблон("%1bot%2/sendAudio", АдресРесурса(), ПараметрыВзаимодействия.Токен);
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("chat_id", ПараметрыВзаимодействия.ИдентификаторПолучатель);
	ПараметрыЗапроса.Вставить("parse_mode", "MarkdownV2");
	ПараметрыЗапроса.Вставить("caption", ТекстовоеОписание);
	
	ДополнительныеПараметры = КоннекторHTTP.НовыеПараметры();
	ДополнительныеПараметры.Данные = ПараметрыЗапроса;
	ДополнительныеПараметры.Файлы = ОписаниеФайла(Файл, "audio");
	
	Возврат КоннекторHTTP.PostJson(URL, Неопределено, ДополнительныеПараметры, Сессия);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Адрес ресурса.
// 
// Возвращаемое значение:
//  Строка - Адрес ресурса
Функция АдресРесурса() 

	Возврат "https://api.telegram.org/";

КонецФункции

// Инициализировать сессию.
// 
// Параметры:
//  Сессия - Структура, Неопределено -  
Процедура ИнициализироватьСессию(Сессия)
	
	Если Сессия = Неопределено Тогда
		Сессия = КоннекторHTTP.СоздатьСессию();	
	КонецЕсли;
	
КонецПроцедуры

Функция ОписаниеФайла(Файл, Имя)
	
	ОписаниеФайла = Новый Структура("Данные,ИмяФайла,Имя");
	ЗаполнитьЗначенияСвойств(ОписаниеФайла, Файл);
	ОписаниеФайла.Имя = Имя;
	
	Возврат ОписаниеФайла;
	
КонецФункции

#КонецОбласти
